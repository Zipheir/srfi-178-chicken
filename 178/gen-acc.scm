(define (%make-bitvector-generator bvec ref-proc)
  (let ((len (bitvector-length bvec))
        (i 0))
    (lambda ()
      (if (= i len)
          (eof-object)
          (let ((r (ref-proc bvec i)))
            (set! i (+ i 1))
            r)))))

(: make-bitvector/int-generator (bitvector -> procedure))
(define (make-bitvector/int-generator bvec)
  (assert (bitvector? bvec))
  (%make-bitvector-generator bvec bitvector-ref/int))

(: make-bitvector/bool-generator (bitvector -> procedure))
(define (make-bitvector/bool-generator bvec)
  (assert (bitvector? bvec))
  (%make-bitvector-generator bvec bitvector-ref/bool))

(: make-bitvector-accumulator (-> procedure))
(define (make-bitvector-accumulator)
  (let ((r '()))
    (lambda (x)
      (if (eof-object? x)
        (reverse-list->bitvector r)
        (set! r (cons x r))))))
